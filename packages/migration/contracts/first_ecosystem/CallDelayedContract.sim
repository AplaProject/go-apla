// +prop AppID = '1'
// +prop Conditions = 'ContractConditions("MainCondition")'
contract CallDelayedContract {
	data {
		Id int
	}

	conditions {
		FullNodeCondition()

		var rows array
		rows = DBFind("@1delayed_contracts").Where({"id": $Id, "deleted": 0})

		if !Len(rows) {
			warning Sprintf(LangRes("@1template_delayed_contract_not_exist", "en"), $Id)
		}
		$cur = rows[0]
		$limit = Int($cur["limit"])
		$counter = Int($cur["counter"])

		if $block < Int($cur["block_id"]) {
			warning Sprintf(LangRes("@1template_delayed_contract_error", "en"), $Id, $cur["block_id"], $block)
		}

		if $limit > 0 && $counter >= $limit {
			warning Sprintf(LangRes("@1template_delayed_contract_limited", "en"), $Id)
		}
	}

	action {
		$counter = $counter + 1

		var block_id int
		block_id = $block
		if $limit == 0 || $limit > $counter {
			block_id = block_id + Int($cur["every_block"])
		}

		DBUpdate("@1delayed_contracts", $Id, {"counter": $counter, "block_id": block_id})

		var params map
		CallContract($cur["contract"], params)
	}
}