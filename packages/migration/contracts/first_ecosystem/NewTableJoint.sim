// +prop AppID = 1
// +prop Conditions = 'ContractConditions("MainCondition")'
contract NewTableJoint {
    data {
        ApplicationId int
        Name string
        ColumnsArr array
        TypesArr array
        InsertPerm string
        UpdatePerm string
        NewColumnPerm string
    }

    conditions {
        var i int
        while i < Len($ColumnsArr) {
            if Size($ColumnsArr[i]) == 0 {
                info("Columns is empty")
            }
            if Size($TypesArr[i]) == 0 {
                info("Type is empty")
            }
            i = i + 1
        }
    }

    action {
        var i int
        var col_arr array
        while i < Len($ColumnsArr) {
            var col_map map
            col_map["name"] = $ColumnsArr[i]
            col_map["type"] = $TypesArr[i]
            col_map["conditions"] = "true"
            col_arr[i] = JSONEncode(col_map)
            i = i + 1
        }

        var Permissions map
        Permissions["insert"] = $InsertPerm 
        Permissions["update"] = $UpdatePerm
        Permissions["new_column"] = $NewColumnPerm

        var params map
        params["ApplicationId"] = $ApplicationId 
        params["Name"] = $Name
        params["Columns"] = JSONEncode(col_arr)
        params["Permissions"] = JSONEncode(Permissions)
        CallContract("NewTable", params)
    }
}